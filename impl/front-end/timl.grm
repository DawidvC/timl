open Ast

%%

%name TiML

%term ID of string
    | LPAREN
    | RPAREN
    | DARROW
    | FN
    | FIX
    | LSQ
    | RSQ
    | LCUR
    | RCUR
    | SUMCASE
    | UNPACK
    | CASE
    | OF
    | COLON
    | RTRI
    | LET
    | IN
    | END
    | COMMA
    | RETURN
    | ARROW
    | DDASH
    | MULT
    | PLUS
    | MU
    | INT of int
    | BAR
    | AND
    | OR
    | IFF
    | EQ
    | LE
    | VAL
    | DATATYPE
    | EOF
    | FORALL
    | EXISTS
    | APPR (* dummy terminal for precedence *)
    | MAX
    | MIN
    | BOGUS of string

%nonterm start of decl list
       | exp of exp
       | aexp of exp
       | ty of ty
         (* | aty of ty *)
       | idx of idx
       | sort of sort
       | bsort of bsort
       | prop of prop
       | id of id
       | exp_comma_star of exp list
       | comma_exp_star of exp list
       | abs of abs
       | bind_plus of bind list
       | bind_star of bind list
       | bind of bind
       | tbind_plus of bind list
       | tbind_star of bind list
       | tbind of bind
       | case_type of case_type
       | return of ty option * idx option
       | rule_bar_star of (ptrn * exp) list
       | bar_rule_star of (ptrn * exp) list
       | rule of ptrn * exp
       | ptrn of ptrn
       | id_cur_space of id list
       | id_plus of id list
       | id_star of id list
       | decl_star of decl list
       | decl of decl
       | quan of quan
       | cur_sort_star of sort list
       (* | sort_plus of sort list *)
       | constr_bar_star of constr_decl list
       | bar_constr_star of constr_decl list
       | constr of constr_decl
       | sorting_star of bind list
       | sorting of bind


%keyword FN FIX CASE SUMCASE UNPACK OF LET IN END RETURN VAL DATATYPE

%pos pos

		(* %nonassoc LET *)
		(* %nonassoc RTRI *)
%left COMMA
%nonassoc IFF
%right DDASH ARROW
%right OR
%right AND
%nonassoc EQ LE
%left PLUS
%left MULT MAX MIN
%right APPR

%eop EOF
%noshift EOF
%nodefault
%verbose

%arg (reporter) : reporter

%%

start : decl_star (decl_star)

decl : VAL id EQ exp (Val (id, exp, (VALleft, expright)))
    | DATATYPE cur_sort_star id_plus EQ constr_bar_star (let val id :: id_star = (map fst o rev) id_plus in Datatype (id, id_star, rev cur_sort_star, constr_bar_star, (DATATYPEleft, constr_bar_starright)) end)

exp : aexp (aexp)
    | exp aexp (App (exp, aexp, (expleft, aexpright)))
    | exp LSQ ty RSQ (AppT (exp, ty, (expleft, tyright)))
    | exp LCUR idx RCUR (AppI (exp, idx, (expleft, idxright)))
    | exp COLON ty (Ascription (exp, ty, (expleft, tyright)))
    | exp RTRI idx (AscriptionTime (exp, idx, (expleft, idxright)))

aexp : id (Var id)
     | LPAREN exp_comma_star RPAREN (Tuple (exp_comma_star, (LPARENleft, RPARENright)))
                (* | LPAREN exp RPAREN (exp) *) (* already covered by previous one *)
     | abs bind_plus DARROW exp (Abs (abs, bind_plus, exp, (absleft, expright)))
     | case_type exp return OF rule_bar_star (Case (case_type, exp, return, rule_bar_star, (case_typeleft, rule_bar_starright)))
     | LET decl_star IN exp END (Let (decl_star, exp, (LETleft, ENDright)))
     | INT (Const (INT, (INTleft, INTright)))

ty : ty ty %prec APPR (AppTT (ty2, ty1, (ty1left, ty2right)))
           | LCUR idx RCUR ty %prec APPR (AppTI (ty, idx, (LCURleft, tyright)))
                              | ty DDASH idx ARROW ty (Arrow (ty1, idx, ty2, (ty1left, ty2right)))
                              | ty MULT ty (Prod (ty1, ty2, (ty1left, ty2right)))
                              | ty PLUS ty (Sum (ty1, ty2, (ty1left, ty2right)))
                (* aty : *)
                              | id (VarT id)
                              | quan tbind_plus COMMA ty (Quan (quan, tbind_plus, ty, (quanleft, tyright)))
                              | MU ID tbind_star COMMA ty (Recur (ID, tbind_star, ty, (MUleft, tyright)))
                              | LPAREN ty RPAREN (ty)

idx : id (VarI id)
    | INT (Tint (INT, (INTleft, INTright)))
    | idx PLUS idx (Tadd (idx1, idx2, (idx1left, idx2right)))
    | idx MULT idx (Tmult (idx1, idx2, (idx1left, idx2right)))
    | idx MAX idx (Tmax (idx1, idx2, (idx1left, idx2right)))
    | idx MIN idx (Tmin (idx1, idx2, (idx1left, idx2right)))
    | LPAREN RPAREN (TTI (LPARENleft, RPARENright))
    | LPAREN idx RPAREN (idx)

sort : bsort (Basic bsort)
     | LCUR id COLON bsort BAR prop RCUR (Subset (bsort, id, prop, (LCURleft, RCURright)))

bsort : id (id)

prop : id (Pconst id)
     | prop AND prop (And (prop1, prop2, (prop1left, prop2right)))
     | prop OR prop (Or (prop1, prop2, (prop1left, prop2right)))
     | prop ARROW prop (Imply (prop1, prop2, (prop1left, prop2right)))
     | prop IFF prop (Iff (prop1, prop2, (prop1left, prop2right)))
     | idx EQ idx (Eq (idx1, idx2, (idx1left, idx2right)))
     | idx LE idx (TimeLe (idx1, idx2, (idx1left, idx2right)))

id : ID ((ID, (IDleft, IDright)))

exp_comma_star : exp comma_exp_star (exp :: comma_exp_star)
	       | ([])

comma_exp_star : COMMA exp comma_exp_star (exp :: comma_exp_star)
	       | ([])

abs : FN (Fn)
    | FIX (Fix)

bind_star : bind bind_star (bind :: bind_star)
	  | ([])
bind_plus : bind bind_star (bind :: bind_star)

bind : id COLON ty (Typing (id, ty, (idleft, tyright)))
     | LSQ id RSQ (Kinding id)
     | LCUR id COLON sort RCUR (Sorting (id, sort, (LCURleft, RCURright)))
     | LPAREN bind RPAREN (bind)

tbind_star : tbind tbind_star (tbind :: tbind_star)
	   | ([])
tbind_plus : tbind tbind_star (tbind :: tbind_star)

tbind : id (Kinding id)
      | LCUR id COLON sort RCUR (Sorting (id, sort, (LCURleft, RCURright)))
      | LPAREN tbind RPAREN (tbind)

case_type : SUMCASE (HSumCase)
          | UNPACK (HUnpack)
          | CASE (HCase)
                 
return : RETURN ty RTRI idx ((SOME ty, SOME idx))
       | RETURN ty ((SOME ty, NONE))
       | RETURN RTRI idx ((NONE, SOME idx))
       | ((NONE, NONE))

rule_bar_star : rule bar_rule_star (rule :: bar_rule_star)
	      | ([])

bar_rule_star : BAR rule bar_rule_star (rule :: bar_rule_star)
	      | ([])

rule : ptrn DARROW exp ((ptrn, exp))

ptrn : id id_cur_space id (Constr (id1, map fst id_cur_space, id2, (id1left, id2right)))
(* ptrn : id_plus id (Constr (hd id_plus, map fst (tl id_plus), id, (id_plusleft, idright))) *)

id_cur_space : LCUR id_plus RCUR (id_plus)
	     | ([])

id_star : id id_star (id :: id_star)
	| ([])
id_plus : id id_star (id :: id_star)

quan : FORALL (Forall)
     | EXISTS (Exists)

decl_star : decl decl_star (decl :: decl_star)
	 | ([])

constr : id OF sorting_star ty ARROW ty ((id, sorting_star, ty1, ty2, (idleft, ty2right)))

cur_sort_star : LCUR sort RCUR cur_sort_star (sort :: cur_sort_star)
	  | ([])
(* sort_plus : sort cur_sort_star (sort :: cur_sort_star) *)

constr_bar_star : constr bar_constr_star (constr :: bar_constr_star)
	        | ([])

bar_constr_star : BAR constr bar_constr_star (constr :: bar_constr_star)
	        | ([])

sorting : LCUR id COLON sort RCUR (Sorting (id, sort, (LCURleft, RCURright)))

sorting_star : sorting sorting_star (sorting :: sorting_star)
	   | ([])
